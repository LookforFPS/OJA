plugins {
    id 'java-library'
    id 'maven-publish'
}

project.group = 'me.lookforfps'
project.description = "OpenAI Java API"
project.version = new Version("1", "0", "2")

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenCentral()
}

dependencies {
    compileOnly libs.lombok
    annotationProcessor libs.lombok
    testCompileOnly libs.lombok
    testAnnotationProcessor libs.lombok

    api libs.jackson.databind
    api libs.slf4j
    api libs.okhttp
}

publishing {
    repositories {
        maven {
            name = "nexus"
            url = uri("https://repo.lookforfps.dev/repository/maven-releases/")
            credentials {
                username = project.findProperty("user") ?: System.getenv("USERNAME")
                password = project.findProperty("token") ?: System.getenv("TOKEN")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            from(components.java)

            pom {
                name = project.name
                description = 'OpenAI Java API'
                url = "https://github.com/LookforFPS/OJA"

                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'http://opensource.org/licenses/MIT'
                        distribution = 'repo'
                    }
                }

                developers {
                    developer {
                        id = "lookforfps"
                        name = "LookforFPS"
                        email = "me@lookforfps.dev"
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/LookforFPS/OJA'
                    developerConnection = 'scm:git:ssh:git@github.com:LookforFPS/OJA'
                    url = "https://github.com/LookforFPS/OJA"
                }
            }
        }
    }
}


class Version {
    String major
    String minor
    String revision
    String classifier

    Version(String major, String minor, String revision, String classifier = null) {
        this.major = major
        this.minor = minor
        this.revision = revision
        this.classifier = classifier
    }

    String toString() {
        return "${major}.${minor}.${revision}${classifier ? '-' + classifier : ''}"
    }
}
